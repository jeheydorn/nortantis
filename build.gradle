import org.gradle.plugins.ide.eclipse.model.internal.FileReferenceFactory

plugins {
    id 'java'
    id 'application'
    id 'com.diffplug.spotless' version '6.23.3'
    id 'eclipse'
}

repositories {
    mavenCentral()
    maven { url = "https://maven.pkg.jetbrains.space/public/p/compose/dev" }
}

def osName = System.getProperty("os.name")
def targetOs = ""
if (osName == "Mac OS X") {
    targetOs = "macos"
} else if (osName.startsWith("Win")) {
    targetOs = "windows"
} else if (osName.startsWith("Linux")) {
    targetOs = "linux"
} else {
    throw new GradleException("Unsupported OS: $osName")
}

def osArch = System.getProperty("os.arch")
def targetArch = ""
if (osArch == "x86_64" || osArch == "amd64") {
    targetArch = "x64"
} else if (osArch == "aarch64") {
    targetArch = "arm64"
} else {
    throw new GradleException("Unsupported arch: $osArch")
}

def skikoVersion = "0.9.37"
def skikoTarget = "${targetOs}-${targetArch}"

// LWJGL for OpenGL context creation (GPU acceleration)
def lwjglVersion = "3.3.6"
def lwjglNatives = ""
if (targetOs == "windows") {
    lwjglNatives = targetArch == "arm64" ? "natives-windows-arm64" : "natives-windows"
} else if (targetOs == "linux") {
    lwjglNatives = targetArch == "arm64" ? "natives-linux-arm64" : "natives-linux"
} else if (targetOs == "macos") {
    lwjglNatives = targetArch == "arm64" ? "natives-macos-arm64" : "natives-macos"
}

dependencies {
    implementation fileTree(dir: 'libraries', include: ['*.jar'])
    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'com.formdev:flatlaf:3.6.2'
    implementation "org.jetbrains.skiko:skiko:$skikoVersion"
    implementation "org.jetbrains.skiko:skiko-awt-runtime-$skikoTarget:$skikoVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.23"

    // LWJGL for GPU context creation
    implementation "org.lwjgl:lwjgl:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-opengl:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-glfw:$lwjglVersion"
    runtimeOnly "org.lwjgl:lwjgl:$lwjglVersion:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl:$lwjglVersion:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw:$lwjglVersion:$lwjglNatives"
}

// Attaches javadocs to libraries in Eclipse.
eclipse {
    classpath {
        file {
            whenMerged { cp ->
                // Add sources to a classpath entry
                def fileReferenceFactory = new FileReferenceFactory()

                def entry = cp.entries.find{ entry -> entry.path.endsWith('JTransforms-3.1-with-dependencies.jar') }
                // add javadoc jar
                entry.javadocPath = fileReferenceFactory.fromPath(file('libraries-doc/JTransforms-3.1-javadoc.jar').toString())
            }
        }
    }
}

application {
    mainClass = 'nortantis.swing.MainWindow'
    applicationDefaultJvmArgs = [
        '--enable-native-access=ALL-UNNAMED',
        //'--add-opens', 'java.base/java.nio=ALL-UNNAMED',
        //'-Dnortantis.gpu.enable=true',
    ]
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'nortantis.swing.MainWindow'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    }
    from(sourceSets.main.output) {
        from('assets') {
            into 'assets'
        }
    }
    archiveFileName = 'Nortantis.jar'
}

test {
    jvmArgs = ['-ea', '--enable-native-access=ALL-UNNAMED', '-Dfile.encoding=UTF-8', '-Dsun.java2d.d3d=false', '-Xmx3g',
               '--add-opens', 'java.base/java.nio=ALL-UNNAMED',
               '-Dnortantis.gpu.enable=true',
               '-Dnortantis.shaders.enable=true',
    ]
    useJUnitPlatform()
    // Fork a new JVM for each test class to ensure complete isolation.
    // This is needed because MapCreatorTest and SkiaMapCreatorTest use different
    // PlatformFactory implementations that share global state.
    forkEvery = 1
}

// Benchmark task with JFR profiling
// Usage: ./gradlew benchmark
// Output: build/profile.jfr (open in JDK Mission Control or IntelliJ)
tasks.register('benchmark', Test) {
    description = 'Run benchmarks with JFR profiling'
    group = 'verification'

    // Use the test source set
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    useJUnitPlatform()
    forkEvery = 1

    // Only run benchmark tests
    filter {
        includeTestsMatching 'nortantis.test.SkiaMapCreatorBenchmark'
        includeTestsMatching 'nortantis.test.AwtMapCreatorBenchmark'
    }

    jvmArgs = [
        '-ea',
        '--enable-native-access=ALL-UNNAMED',
        '-Dfile.encoding=UTF-8',
        '-Dsun.java2d.d3d=false',
        '-Xmx4g',
        '--add-opens', 'java.base/java.nio=ALL-UNNAMED',
        //'-Dnortantis.gpu.enable=true',
        '-Dnortantis.shaders.enable=true',
        // JFR profiling - records to build/profile.jfr
        '-XX:StartFlightRecording=filename=build/profile.jfr,settings=profile',
    ]

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    doFirst {
        println "Starting benchmark with JFR profiling..."
        println "JFR output will be saved to: build/profile.jfr"
    }

    doLast {
        println "\nBenchmark complete!"
        println "Open build/profile.jfr in JDK Mission Control or IntelliJ to analyze."
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
            include '**/*.java'
        }
    }
    test {
        java {
            srcDirs = ['test']
            include '**/*.java'
        }
    }
}

spotless {
    java {
        eclipse().configFile 'eclipse-formatter-config.xml'
        cleanthat()
    }
}
