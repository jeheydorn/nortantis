plugins {
    id 'java'
    id 'application'
    id 'com.diffplug.spotless' version '6.23.3'
    id 'eclipse'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libraries', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
}

// Attaches javadocs to libraries in Eclipse.
eclipse {
    classpath {
        file {
            whenMerged { cp ->
                // Add sources to a classpath entry
                def fileReferenceFactory = new org.gradle.plugins.ide.eclipse.model.internal.FileReferenceFactory()

                def entry = cp.entries.find{ entry -> entry.path.endsWith('JTransforms-3.1-with-dependencies.jar') }
                // add javadoc jar
                entry.javadocPath = fileReferenceFactory.fromPath(file('libraries-doc/JTransforms-3.1-javadoc.jar').toString())
            }
        }
    }
}

application {
    mainClass = 'nortantis.swing.MainWindow'
}

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'nortantis.swing.MainWindow'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    }
	from(sourceSets.main.output) {
	    from('assets') {
	        into 'assets'
	    }
	}
	archiveFileName = 'Nortantis.jar' 
}

test {
	jvmArgs = ['-Dfile.encoding=UTF-8', '-Xmx3g']
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
            include '**/*.java'
        }
    }
    test {
        java {
            srcDirs = ['test']
            include '**/*.java'
        }
    }
}

spotless {
    java {
        eclipse().configFile 'eclipse-formatter-config.xml'
    }
}


// Define a task to format a specific file
task formatSpecificFile {
    doLast {
        def filePath = project.hasProperty('filePath') ? project.filePath : 'src/nortantis/Background.java'
        println "Formatting file: $filePath"
        javaexec {
            main = 'com.diffplug.spotless.Main'
            classpath = configurations.spotless
            args 'apply', '--java', filePath
        }
    }
}
