import org.gradle.plugins.ide.eclipse.model.internal.FileReferenceFactory

plugins {
    id 'java'
    id 'application'
    id 'com.diffplug.spotless' version '6.23.3'
    id 'eclipse'
}

repositories {
    mavenCentral()
    maven { url "https://maven.pkg.jetbrains.space/public/p/compose/dev" }
}

def osName = System.getProperty("os.name")
def targetOs = ""
if (osName == "Mac OS X") {
    targetOs = "macos"
} else if (osName.startsWith("Win")) {
    targetOs = "windows"
} else if (osName.startsWith("Linux")) {
    targetOs = "linux"
} else {
    throw new GradleException("Unsupported OS: $osName")
}

def osArch = System.getProperty("os.arch")
def targetArch = ""
if (osArch == "x86_64" || osArch == "amd64") {
    targetArch = "x64"
} else if (osArch == "aarch64") {
    targetArch = "arm64"
} else {
    throw new GradleException("Unsupported arch: $osArch")
}

def skikoVersion = "0.9.37"
def skikoTarget = "${targetOs}-${targetArch}"

dependencies {
    implementation fileTree(dir: 'libraries', include: ['*.jar'])
    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'com.formdev:flatlaf:3.6.2'
    implementation "org.jetbrains.skiko:skiko:$skikoVersion"
    implementation "org.jetbrains.skiko:skiko-awt-runtime-$skikoTarget:$skikoVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.23"
}

// Attaches javadocs to libraries in Eclipse.
eclipse {
    classpath {
        file {
            whenMerged { cp ->
                // Add sources to a classpath entry
                def fileReferenceFactory = new FileReferenceFactory()

                def entry = cp.entries.find{ entry -> entry.path.endsWith('JTransforms-3.1-with-dependencies.jar') }
                // add javadoc jar
                entry.javadocPath = fileReferenceFactory.fromPath(file('libraries-doc/JTransforms-3.1-javadoc.jar').toString())
            }
        }
    }
}

application {
    mainClass = 'nortantis.swing.MainWindow'
}

java {
    sourceCompatibility = JavaVersion.VERSION_24
    targetCompatibility = JavaVersion.VERSION_25
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'nortantis.swing.MainWindow'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    }
    from(sourceSets.main.output) {
        from('assets') {
            into 'assets'
        }
    }
    archiveFileName = 'Nortantis.jar'
}

test {
    jvmArgs = ['-Dfile.encoding=UTF-8', '-Xmx3g']
    useJUnitPlatform()
    // Fork a new JVM for each test class to ensure complete isolation.
    // This is needed because MapCreatorTest and SkiaMapCreatorTest use different
    // PlatformFactory implementations that share global state.
    forkEvery = 1
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
            include '**/*.java'
        }
    }
    test {
        java {
            srcDirs = ['test']
            include '**/*.java'
        }
    }
}

spotless {
    java {
        eclipse().configFile 'eclipse-formatter-config.xml'
        cleanthat()
    }
}
